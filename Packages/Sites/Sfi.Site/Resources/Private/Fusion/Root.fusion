include: OverrideStock.ts2
include: TypoScriptObjects/**/*.ts2
include: NodeTypes/**/*.ts2

# This is a default page object.
prototype(Sfi.Site:Root) < prototype(Neos.Neos:Page) {
	head {
		titleTag.content = ${String.stripTags(q(node).property('title'))}
		stylesheets.site = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Site/Private/Partials/SharedResources.html'
			sectionName = 'stylesheets'
		}
		javascripts.headScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Site/Private/Partials/SharedResources.html'
			sectionName = 'headScripts'
		}
	}
	htmlTag.attributes.lang = ${Array.first(node.context.dimensions.language)}
	bodyTag.attributes.class = 'Page'
	bodyTag.attributes.id = ${'Page-' + q(node).property('_name')}
	body {
		templatePath = 'resource://Sfi.Site/Private/Templates/Root.html'
		partialRootPath = 'resource://Sfi.Site/Private/Partials'
		layoutRootPath = 'resource://Sfi.Site/Private/Layouts'

		sectionMenu = Sfi.Site:SectionMenu

		main = PrimaryContent {
			renderByType {
				condition = TRUE
				type = ${q(node).property('_nodeType.name')}
			}
		}

		javascripts.bodyScripts = Neos.Fusion:Template {
			templatePath = 'resource://Sfi.Site/Private/Partials/SharedResources.html'
			partialRootPath = 'resource://Sfi.Site/Private/Partials'
			sectionName = 'bodyScripts'
			@if.isLive = ${node.context.workspaceName == 'live'}
		}
	}
}

root.fallbackPage {
	@position = 'before default'
	condition = TRUE
	type = 'Sfi.Site:Root'
}

# Disable stupid layout mechanism
root.layout>
